app OpenSaaS {
  wasp: {
    version: "^0.16.0"
  },

  title: "My Open SaaS App",

  head: [
    "<meta charset='utf-8' />",
    "<meta name='description' content='Your apps main description and features.' />",
    "<meta name='author' content='Your (App) Name' />",
    "<meta name='keywords' content='saas, solution, product, app, service' />",
    
    "<meta property='og:type' content='website' />",
    "<meta property='og:title' content='Your Open SaaS App' />",
    "<meta property='og:site_name' content='Your Open SaaS App' />",
    "<meta property='og:url' content='https://your-saas-app.com' />",
    "<meta property='og:description' content='Your apps main description and features.' />",
    "<meta property='og:image' content='https://your-saas-app.com/public-banner.webp' />",
    "<meta name='twitter:image' content='https://your-saas-app.com/public-banner.webp' />",
    "<meta name='twitter:image:width' content='800' />",
    "<meta name='twitter:image:height' content='400' />",
    "<meta name='twitter:card' content='summary_large_image' />",
    "<script defer data-domain='<your-site-id>' src='https://plausible.io/js/script.js'></script>",
    "<script defer data-domain='<your-site-id>' src='https://plausible.io/js/script.local.js'></script>",
  ],

  auth: {
    userEntity: User,
    methods: {
      email: {
        fromField: {
          name: "Open SaaS App",
          email: "me@example.com"
        },
        emailVerification: {
          clientRoute: EmailVerificationRoute,
          getEmailContentFn: import { getVerificationEmailContent } from "@src/auth/email-and-pass/emails",
        },
        passwordReset: {
          clientRoute: PasswordResetRoute,
          getEmailContentFn: import { getPasswordResetEmailContent } from "@src/auth/email-and-pass/emails",
        },
        userSignupFields: import { getEmailUserFields } from "@src/auth/userSignupFields",
      },
    },
    onAuthFailedRedirectTo: "/login",
    onAuthSucceededRedirectTo: "/demo-app",
  },

  db: {
    seeds: [
      import { seedMockUsers } from "@src/server/scripts/dbSeeds",
    ]
  },

  client: {
    rootComponent: import App from "@src/client/App",
  },

  emailSender: {
    provider: SMTP,
    defaultFrom: {
      name: "WineClub SaaS",
      email: "noreply@wineclub.com"
    },
  },
}

route LandingPageRoute { path: "/", to: LandingPage }
page LandingPage {
  component: import LandingPage from "@src/landing-page/LandingPage"
}

//#region Auth Pages
route LoginRoute { path: "/login", to: LoginPage }
page LoginPage {
  component: import Login from "@src/client/pages/auth/LoginPage"
}

route SignupRoute { path: "/signup", to: SignupPage }
page SignupPage {
  component: import { Signup } from "@src/client/pages/auth/SignupPage"
}

route RequestPasswordResetRoute { path: "/request-password-reset", to: RequestPasswordResetPage }
page RequestPasswordResetPage {
  component: import { RequestPasswordResetPage } from "@src/client/pages/auth/RequestPasswordResetPage",
}

route PasswordResetRoute { path: "/password-reset", to: PasswordResetPage }
page PasswordResetPage {
  component: import { PasswordResetPage } from "@src/client/pages/auth/PasswordResetPage",
}

route EmailVerificationRoute { path: "/email-verification", to: EmailVerificationPage }
page EmailVerificationPage {
  component: import { EmailVerificationPage } from "@src/client/pages/auth/EmailVerificationPage",
}
//#endregion

//#region User
route AccountRoute { path: "/account", to: AccountPage }
page AccountPage {
  authRequired: true,
  component: import Account from "@src/client/pages/user/AccountPage"
}

query getPaginatedUsers {
  fn: import { getPaginatedUsers } from "@src/user/operations",
  entities: [User]
}

action updateIsUserAdminById {
  fn: import { updateIsUserAdminById } from "@src/user/operations",
  entities: [User]
}
//#endregion

//#region Demo AI App
route DemoAppRoute { path: "/demo-app", to: DemoAppPage }
page DemoAppPage {
  authRequired: true,
  component: import DemoAppPage from "@src/demo-ai-app/DemoAppPage"
}

action generateGptResponse {
  fn: import { generateGptResponse } from "@src/demo-ai-app/operations",
  entities: [User, Task, GptResponse]
}

action createTask {
  fn: import { createTask } from "@src/demo-ai-app/operations",
  entities: [Task]
}

action deleteTask {
  fn: import { deleteTask } from "@src/demo-ai-app/operations",
  entities: [Task]
}

action updateTask {
  fn: import { updateTask } from "@src/demo-ai-app/operations",
  entities: [Task]
}

query getGptResponses {
  fn: import { getGptResponses } from "@src/demo-ai-app/operations",
  entities: [User, GptResponse]
}

query getAllTasksByUser {
  fn: import { getAllTasksByUser } from "@src/demo-ai-app/operations",
  entities: [Task]
}
//#endregion

//#region Payment
route PricingPageRoute { path: "/pricing", to: PricingPage }
page PricingPage {
  component: import PricingPage from "@src/client/pages/PricingPage"
}

route CheckoutRoute { path: "/checkout", to: CheckoutPage }
page CheckoutPage {
  authRequired: true,
  component: import Checkout from "@src/client/pages/CheckoutPage"
}

query getCustomerPortalUrl {
  fn: import { getCustomerPortalUrl } from  "@src/payment/operations",
  entities: [User]
}

action generateCheckoutSession {
  fn: import { generateCheckoutSession } from "@src/payment/operations",
  entities: [User]
}

api paymentsWebhook {
  fn: import { paymentsWebhook } from "@src/payment/webhook",
  entities: [User],
  middlewareConfigFn: import { paymentsMiddlewareConfigFn } from "@src/payment/webhook",
  httpRoute: (POST, "/payments-webhook")
}
//#endregion

//#region File Upload
route FileUploadRoute { path: "/file-upload", to: FileUploadPage }
page FileUploadPage {
  authRequired: true,
  component: import FileUpload from "@src/file-upload/FileUploadPage"
}

action createFile {
  fn: import { createFile } from "@src/file-upload/operations",
  entities: [User, File]
}

query getAllFilesByUser {
  fn: import { getAllFilesByUser } from "@src/file-upload/operations",
  entities: [User, File]
}

query getDownloadFileSignedURL {
  fn: import { getDownloadFileSignedURL } from "@src/file-upload/operations",
  entities: [User, File]
}
//#endregion

//#region Analytics
query getDailyStats {
  fn: import { getDailyStats } from "@src/analytics/operations",
  entities: [User, DailyStats]
}

job dailyStatsJob {
  executor: PgBoss,
  perform: {
    fn: import { calculateDailyStats } from "@src/analytics/stats"
  },
  schedule: {
    cron: "0 * * * *"
  },
  entities: [User, DailyStats, Logs, PageViewSource]
}
//#endregion

//#region Admin Dashboard
route AdminRoute { path: "/admin", to: AnalyticsDashboardPage }
page AnalyticsDashboardPage {
  authRequired: true,
  component: import AnalyticsDashboardPage from "@src/admin/dashboards/analytics/AnalyticsDashboardPage"
}

route AdminUsersRoute { path: "/admin/users", to: AdminUsersPage }
page AdminUsersPage {
  authRequired: true,
  component: import AdminUsers from "@src/admin/dashboards/users/UsersDashboardPage"
}

route AdminSettingsRoute { path: "/admin/settings", to: AdminSettingsPage }
page AdminSettingsPage {
  authRequired: true,
  component: import AdminSettings from "@src/admin/elements/settings/SettingsPage"
}

route AdminChartsRoute { path: "/admin/chart", to: AdminChartsPage }
page AdminChartsPage {
  authRequired: true,
  component: import AdminCharts from "@src/admin/elements/charts/ChartsPage"
}

route AdminFormElementsRoute { path: "/admin/forms/form-elements", to: AdminFormElementsPage }
page AdminFormElementsPage {
  authRequired: true,
  component: import FormElements from "@src/admin/elements/forms/FormElementsPage"
}

route AdminFormLayoutsRoute { path: "/admin/forms/form-layouts", to: AdminFormLayoutsPage }
page AdminFormLayoutsPage {
  authRequired: true,
  component: import FormLayout from "@src/admin/elements/forms/FormLayoutsPage"
}

route AdminCalendarRoute { path: "/admin/calendar", to: AdminCalendarPage }
page AdminCalendarPage {
  authRequired: true,
  component: import AdminCalendar from "@src/admin/elements/calendar/CalendarPage"
}

route AdminUIElementsRoute { path: "/admin/ui", to: AdminUIElementsPage }
page AdminUIElementsPage {
  authRequired: true,
  component: import Buttons from "@src/admin/elements/ui-elements/ButtonsPage"
}

route AdminUIAlertsRoute { path: "/admin/ui/alerts", to: AdminUIAlertsPage }
page AdminUIAlertsPage {
  authRequired: true,
  component: import Alerts from "@src/admin/elements/ui-elements/AlertsPage"
}

route NotFoundRoute { path: "*", to: NotFoundPage }
page NotFoundPage {
  component: import { NotFoundPage } from "@src/client/components/NotFoundPage"
}
//#endregion

//#region Contact Form Messages
route AdminMessagesRoute { path: "/admin/messages", to: AdminMessagesPage }
page AdminMessagesPage {
  authRequired: true,
  component: import AdminMessages from "@src/messages/MessagesPage"
}
//#endregion

//#region Wine Cave Management
route WineCaveDashboardRoute { path: "/wine-caves", to: WineCaveDashboardPage }
page WineCaveDashboardPage {
  authRequired: true,
  component: import WineCaveDashboardPage from "@src/client/pages/wine-cave/WineCaveDashboardPage"
}

route CreateWineCaveRoute { path: "/wine-caves/create", to: CreateWineCavePage }
page CreateWineCavePage {
  authRequired: true,
  component: import CreateWineCavePage from "@src/client/pages/wine-cave/CreateWineCavePage"
}

route WineCaveAnalyticsRoute { path: "/wine-cave-analytics", to: WineCaveAnalyticsPage }
page WineCaveAnalyticsPage {
  authRequired: true,
  component: import AnalyticsDashboardPage from "@src/client/pages/wine-cave/AnalyticsDashboardPage"
}

query getWineCaves {
  fn: import { getWineCaves } from "@src/wine-cave/operations",
  entities: [User, WineCave, SubscriptionTier, Wine]
}

action createWineCave {
  fn: import { createWineCave } from "@src/wine-cave/operations",
  entities: [User, WineCave]
}

action updateWineCave {
  fn: import { updateWineCave } from "@src/wine-cave/operations",
  entities: [User, WineCave]
}

action deleteWineCave {
  fn: import { deleteWineCave } from "@src/wine-cave/operations",
  entities: [User, WineCave, WineSubscription]
}

action createSubscriptionTier {
  fn: import { createSubscriptionTier } from "@src/wine-cave/operations",
  entities: [User, WineCave, SubscriptionTier]
}

action createWine {
  fn: import { createWine } from "@src/wine-cave/operations",
  entities: [User, WineCave, Wine]
}
//#endregion

//#region Wine Subscriptions
route WineSubscriptionsRoute { path: "/subscriptions", to: WineSubscriptionsPage }
page WineSubscriptionsPage {
  authRequired: true,
  component: import WineSubscriptions from "@src/client/pages/wine-subscriptions/WineSubscriptionsPage"
}

route MemberPortalRoute { path: "/member-portal", to: MemberPortalPage }
page MemberPortalPage {
  authRequired: true,
  component: import MemberPortal from "@src/client/pages/wine-subscriptions/MemberPortalPage"
}

route SubscribeToWineCaveRoute { path: "/wine-caves/:id/subscribe", to: SubscribeToWineCavePage }
page SubscribeToWineCavePage {
  authRequired: true,
  component: import SubscribeToWineCave from "@src/client/pages/wine-subscriptions/SubscribeToWineCavePage"
}

route PremiumSubscriptionRoute { path: "/premium-subscription", to: PremiumSubscriptionPage }
page PremiumSubscriptionPage {
  component: import PremiumSubscriptionPage from "@src/client/pages/wine-subscriptions/PremiumSubscriptionPage"
}

route SubscriptionRoute { path: "/subscription", to: SubscriptionPage }
page SubscriptionPage {
  component: import SubscriptionPage from "@src/client/pages/SubscriptionPage"
}

action createWineSubscription {
  fn: import { createWineSubscription } from "@src/wine-subscriptions/operations",
  entities: [User, WineCave, SubscriptionTier, WineSubscription]
}

action updateWineSubscription {
  fn: import { updateWineSubscription } from "@src/wine-subscriptions/operations",
  entities: [User, WineSubscription]
}

query getMyWineSubscriptions {
  fn: import { getMyWineSubscriptions } from "@src/wine-subscriptions/operations",
  entities: [User, WineSubscription, WineCave, SubscriptionTier]
}

query getPublicWineCaves {
  fn: import { getPublicWineCaves } from "@src/wine-subscriptions/operations",
  entities: [WineCave, SubscriptionTier, Wine]
}
//#endregion

//#region Shipping Management
route ShippingDashboardRoute { path: "/shipping", to: ShippingDashboardPage }
page ShippingDashboardPage {
  authRequired: true,
  component: import ShippingDashboard from "@src/shipping/ShippingDashboardPage"
}

action createShipment {
  fn: import { createShipment } from "@src/shipping/operations",
  entities: [User, WineSubscription, Shipment, ShipmentItem]
}

action generateShippingLabel {
  fn: import { generateShippingLabel } from "@src/shipping/operations",
  entities: [User, Shipment]
}

action updateShipment {
  fn: import { updateShipment } from "@src/shipping/operations",
  entities: [User, Shipment]
}

query trackShipment {
  fn: import { trackShipment } from "@src/shipping/operations",
  entities: [User, Shipment, WineSubscription]
}
//#endregion

//#region Loyalty & Referral System
route LoyaltyDashboardRoute { path: "/loyalty", to: LoyaltyDashboardPage }
page LoyaltyDashboardPage {
  authRequired: true,
  component: import LoyaltyDashboard from "@src/loyalty/LoyaltyDashboardPage"
}

action awardPoints {
  fn: import { awardPoints } from "@src/loyalty/operations",
  entities: [User, WineSubscription, LoyaltyPoints, WineCave]
}

action redeemPoints {
  fn: import { redeemPoints } from "@src/loyalty/operations",
  entities: [User, WineSubscription, LoyaltyPoints, WineCave]
}

action createReferral {
  fn: import { createReferral } from "@src/loyalty/operations",
  entities: [User, Referral]
}

query getLoyaltyPointsHistory {
  fn: import { getLoyaltyPointsHistory } from "@src/loyalty/operations",
  entities: [User, LoyaltyPoints, WineSubscription, WineCave]
}

query getReferrals {
  fn: import { getReferrals } from "@src/loyalty/operations",
  entities: [User, Referral]
}

query getAvailableRewards {
  fn: import { getAvailableRewards } from "@src/loyalty/operations",
  entities: [User]
}
//#endregion

//#region Onboarding Wizard
route OnboardingWizardRoute { path: "/onboarding", to: OnboardingWizardPage }
page OnboardingWizardPage {
  authRequired: true,
  component: import OnboardingWizard from "@src/onboarding/OnboardingWizardPage"
}
//#endregion 